import QtQuick 2.9
import QtQuick.Controls 2.2
import QtQuick.Layouts 1.2
import QtQuick.Window 2.2

import StreamingPreferences 1.0
import StreamProfileManager 1.0
import SdlGamepadKeyNavigation 1.0
import SystemProperties 1.0

Flickable {
    id: settingsPage
    property StreamProfileEditor profileEditor
    property var preferences: profileEditor ? profileEditor.settings : StreamingPreferences
    property string closeAction: "back"
    property string pendingProfileId: ""
    property bool refreshingProfileControls: false
    objectName: profileEditor && profileEditor.templateMode ?
                    qsTr("Default Streaming Profile") :
                    (profileEditor ? profileEditor.name : qsTr("Streaming Profile"))

    signal languageChanged()

    function refreshProfileControls() {
        resolutionComboBox.selectSavedValue()
        fpsComboBox.reinitialize()
        slider.value = preferences.bitrateKbps
        windowModeComboBox.reinitialize()
        audioComboBox.reinitialize()
        captureSysKeysModeComboBox.reinitialize()
        decoderComboBox.reinitialize()
        codecComboBox.reinitialize()
    }

    function requestClose() {
        if (profileEditor && profileEditor.dirty) {
            closeAction = "back"
            unsavedChangesDialog.open()
            return true
        }
        return false
    }

    function requestApplicationClose() {
        if (profileEditor && profileEditor.dirty) {
            closeAction = "quit"
            unsavedChangesDialog.open()
            return true
        }
        return false
    }

    function requestGlobalSettings() {
        if (profileEditor && profileEditor.dirty) {
            closeAction = "global"
            unsavedChangesDialog.open()
        } else {
            openGlobalSettings()
        }
        return true
    }

    function requestProfileSwitch(profileId) {
        if (!profileEditor || profileEditor.templateMode ||
                (!profileEditor.newProfile &&
                 profileEditor.profileId === profileId)) {
            return
        }

        pendingProfileId = profileId
        closeAction = "switchProfile"
        if (profileEditor.dirty) {
            unsavedChangesDialog.open()
        } else {
            finishCloseAction()
        }
    }

    function requestNewProfile() {
        if (!profileEditor || profileEditor.templateMode) {
            return
        }

        pendingProfileId = ""
        closeAction = "newProfile"
        if (profileEditor.dirty) {
            unsavedChangesDialog.open()
        } else {
            finishCloseAction()
        }
    }

    function openGlobalSettings() {
        stackView.pop()
        window.navigateTo("qrc:/gui/AppSettingsView.qml", AppSettingsView)
    }

    function finishCloseAction() {
        if (closeAction === "quit") {
            Qt.quit()
        } else if (closeAction === "global") {
            openGlobalSettings()
        } else if (closeAction === "switchProfile") {
            refreshingProfileControls = true
            if (profileEditor.switchToProfile(pendingProfileId)) {
                refreshProfileControls()
            }
            refreshingProfileControls = false
            pendingProfileId = ""
            closeAction = "back"
            contentY = 0
        } else if (closeAction === "newProfile") {
            refreshingProfileControls = true
            if (profileEditor.beginNewProfile()) {
                refreshProfileControls()
            }
            refreshingProfileControls = false
            closeAction = "back"
            contentY = 0
        } else {
            stackView.pop()
        }
    }

    function saveAndClose() {
        if (profileEditor.save()) {
            finishCloseAction()
        } else {
            invalidNameDialog.open()
        }
    }

    boundsBehavior: Flickable.OvershootBounds

    contentWidth: settingsColumn1.width > settingsColumn2.width ? settingsColumn1.width : settingsColumn2.width
    contentHeight: settingsColumn1.height > settingsColumn2.height ? settingsColumn1.height : settingsColumn2.height

    ScrollBar.vertical: ScrollBar {
        anchors {
            left: parent.right
            leftMargin: -10
        }
    }

    function isChildOfFlickable(item) {
        while (item) {
            if (item.parent === contentItem) {
                return true
            }

            item = item.parent
        }
        return false
    }

    NumberAnimation on contentY {
        id: autoScrollAnimation
        duration: 100
    }

    Window.onActiveFocusItemChanged: {
        var item = Window.activeFocusItem
        if (item) {
            // Ignore non-child elements like the toolbar buttons
            if (!isChildOfFlickable(item)) {
                return
            }

            // Map the focus item's position into our content item's coordinate space
            var pos = item.mapToItem(contentItem, 0, 0)

            // Ensure some extra space is visible around the element we're scrolling to
            var scrollMargin = height > 100 ? 50 : 0

            if (pos.y - scrollMargin < contentY) {
                autoScrollAnimation.from = contentY
                autoScrollAnimation.to = Math.max(pos.y - scrollMargin, 0)
                autoScrollAnimation.start()
            }
            else if (pos.y + item.height + scrollMargin > contentY + height) {
                autoScrollAnimation.from = contentY
                autoScrollAnimation.to = Math.min(pos.y + item.height + scrollMargin - height, contentHeight - height)
                autoScrollAnimation.start()
            }
        }
    }

    StackView.onActivated: {
        // This enables Tab and BackTab based navigation rather than arrow keys.
        // It is required to shift focus between controls on the settings page.
        SdlGamepadKeyNavigation.setUiNavMode(true)

        // Highlight the first item if a gamepad is connected
        if (SdlGamepadKeyNavigation.getConnectedGamepads() > 0) {
            resolutionComboBox.forceActiveFocus(Qt.TabFocus)
        }
    }

    StackView.onDeactivating: {
        SdlGamepadKeyNavigation.setUiNavMode(false)
    }

    Column {
        padding: 10
        id: settingsColumn1
        width: settingsPage.width / 2
        spacing: 15

        GroupBox {
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" +
                   (profileEditor.templateMode ? qsTr("Default Profile Template") : qsTr("Profile")) +
                   "</font>"

            Column {
                anchors.fill: parent
                spacing: 8

                Button {
                    width: parent.width
                    visible: !profileEditor.templateMode
                    text: profileEditor.newProfile ?
                              qsTr("New profile draft: %1   …")
                                  .arg(profileEditor.name) :
                              qsTr("Active profile: %1   …")
                                  .arg(profileEditor.name)
                    onClicked: {
                        profilePickerDialog.refresh()
                        profilePickerDialog.open()
                    }
                }

                TextField {
                    id: profileNameField
                    width: parent.width
                    visible: !profileEditor.templateMode
                    text: profileEditor.name
                    placeholderText: qsTr("Profile name")
                    onTextEdited: profileEditor.name = text
                }

                Flow {
                    width: parent.width
                    spacing: 8

                    Button {
                        text: qsTr("Save")
                        enabled: profileEditor.dirty
                        onClicked: {
                            if (!profileEditor.save()) {
                                invalidNameDialog.open()
                            }
                        }
                    }
                    Button {
                        text: qsTr("Copy")
                        visible: !profileEditor.templateMode
                        enabled: !profileEditor.newProfile && !profileEditor.dirty
                        onClicked: profileEditor.copy()
                    }
                    Button {
                        text: qsTr("Delete")
                        visible: !profileEditor.templateMode
                        enabled: profileEditor.canDelete
                        onClicked: deleteProfileDialog.open()
                    }
                    Button {
                        text: qsTr("Set as Default")
                        visible: !profileEditor.templateMode
                        enabled: !profileEditor.newProfile && !profileEditor.dirty
                        onClicked: profileEditor.setAsDefault()
                    }
                    Button {
                        text: qsTr("Reset to Stock")
                        onClicked: {
                            settingsPage.refreshingProfileControls = true
                            profileEditor.resetToStock()
                            settingsPage.refreshProfileControls()
                            settingsPage.refreshingProfileControls = false
                        }
                    }
                }
            }
        }

        GroupBox {
            id: basicSettingsGroupBox
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" + qsTr("Basic Settings") + "</font>"
            font.pointSize: 12

            Column {
                anchors.fill: parent
                spacing: 5

                Label {
                    width: parent.width
                    id: resFPStitle
                    text: qsTr("Resolution and FPS")
                    font.pointSize: 12
                    wrapMode: Text.Wrap
                }

                Label {
                    width: parent.width
                    id: resFPSdesc
                    text: qsTr("Setting values too high for your PC or network connection may cause lag, stuttering, or errors.")
                    font.pointSize: 9
                    wrapMode: Text.Wrap
                }

                Row {
                    spacing: 5
                    width: parent.width

                    AutoResizingComboBox {
                        property int lastIndexValue

                        function addDetectedResolution(friendlyNamePrefix, rect) {
                            var indexToAdd = 0
                            for (var j = 0; j < resolutionComboBox.count; j++) {
                                var existing_width = parseInt(resolutionListModel.get(j).video_width);
                                var existing_height = parseInt(resolutionListModel.get(j).video_height);

                                if (rect.width === existing_width && rect.height === existing_height) {
                                    // Duplicate entry, skip
                                    indexToAdd = -1
                                    break
                                }
                                else if (rect.width * rect.height > existing_width * existing_height) {
                                    // Candidate entrypoint after this entry
                                    indexToAdd = j + 1
                                }
                            }

                            // Insert this display's resolution if it's not a duplicate
                            if (indexToAdd >= 0) {
                                resolutionListModel.insert(indexToAdd,
                                                           {
                                                               "text": friendlyNamePrefix+" ("+rect.width+"x"+rect.height+")",
                                                               "video_width": ""+rect.width,
                                                               "video_height": ""+rect.height,
                                                               "is_custom": false
                                                           })
                            }
                        }

                        function selectSavedValue() {
                            var savedWidth = settingsPage.preferences.width
                            var savedHeight = settingsPage.preferences.height
                            var matchingIndex = -1
                            var customIndex = -1

                            for (var i = 0; i < resolutionListModel.count; i++) {
                                var item = resolutionListModel.get(i)
                                if (item.is_custom) {
                                    customIndex = i
                                }
                                else if (savedWidth === parseInt(item.video_width) &&
                                         savedHeight === parseInt(item.video_height)) {
                                    matchingIndex = i
                                }
                            }

                            if (customIndex < 0) {
                                resolutionListModel.append({
                                    "text": qsTr("Custom"),
                                    "video_width": "",
                                    "video_height": "",
                                    "is_custom": true
                                })
                                customIndex = resolutionListModel.count - 1
                            }

                            if (matchingIndex >= 0) {
                                resolutionListModel.setProperty(customIndex, "text", qsTr("Custom"))
                                resolutionListModel.setProperty(customIndex, "video_width", "")
                                resolutionListModel.setProperty(customIndex, "video_height", "")
                                currentIndex = matchingIndex
                            }
                            else {
                                resolutionListModel.setProperty(
                                            customIndex, "text",
                                            qsTr("Custom") + " (" + savedWidth + "x" + savedHeight + ")")
                                resolutionListModel.setProperty(customIndex, "video_width", "" + savedWidth)
                                resolutionListModel.setProperty(customIndex, "video_height", "" + savedHeight)
                                currentIndex = customIndex
                            }

                            recalculateWidth()
                            lastIndexValue = currentIndex
                        }

                        // ignore setting the index at first, and actually set it when the component is loaded
                        Component.onCompleted: {
                            // Refresh display data before using it to build the list
                            SystemProperties.refreshDisplays()

                            // Add native and safe area resolutions for all attached displays
                            var done = false
                            for (var displayIndex = 0; !done; displayIndex++) {
                                var screenRect = SystemProperties.getNativeResolution(displayIndex);
                                var safeAreaRect = SystemProperties.getSafeAreaResolution(displayIndex);

                                if (screenRect.width === 0) {
                                    // Exceeded max count of displays
                                    done = true
                                    break
                                }

                                addDetectedResolution(qsTr("Native"), screenRect)
                                addDetectedResolution(qsTr("Native (Excluding Notch)"), safeAreaRect)
                            }

                            // Prune resolutions that are over the decoder's maximum
                            var max_pixels = SystemProperties.maximumResolution.width * SystemProperties.maximumResolution.height;
                            if (max_pixels > 0) {
                                for (var j = 0; j < resolutionComboBox.count; j++) {
                                    var existing_width = parseInt(resolutionListModel.get(j).video_width);
                                    var existing_height = parseInt(resolutionListModel.get(j).video_height);

                                    if (existing_width * existing_height > max_pixels) {
                                        resolutionListModel.remove(j)
                                        j--
                                    }
                                }
                            }

                            selectSavedValue()
                        }

                        id: resolutionComboBox
                        maximumWidth: parent.width / 2
                        textRole: "text"
                        model: ListModel {
                            id: resolutionListModel
                            // Other elements may be added at runtime
                            // based on attached display resolution
                            ListElement {
                                text: qsTr("720p")
                                video_width: "1280"
                                video_height: "720"
                                is_custom: false
                            }
                            ListElement {
                                text: qsTr("1080p")
                                video_width: "1920"
                                video_height: "1080"
                                is_custom: false
                            }
                            ListElement {
                                text: qsTr("1440p")
                                video_width: "2560"
                                video_height: "1440"
                                is_custom: false
                            }
                            ListElement {
                                text: qsTr("4K")
                                video_width: "3840"
                                video_height: "2160"
                                is_custom: false
                            }
                        }

                        function updateBitrateForSelection() {
                            var selectedWidth = parseInt(resolutionListModel.get(currentIndex).video_width)
                            var selectedHeight = parseInt(resolutionListModel.get(currentIndex).video_height)

                            // Only modify the bitrate if the values actually changed
                            if (settingsPage.preferences.width !== selectedWidth || settingsPage.preferences.height !== selectedHeight) {
                                settingsPage.preferences.width = selectedWidth
                                settingsPage.preferences.height = selectedHeight

                                if (settingsPage.preferences.autoAdjustBitrate) {
                                    settingsPage.preferences.bitrateKbps = StreamingPreferences.getDefaultBitrate(settingsPage.preferences.width,
                                                                                                              settingsPage.preferences.height,
                                                                                                              settingsPage.preferences.fps,
                                                                                                              settingsPage.preferences.enableYUV444);
                                    slider.value = settingsPage.preferences.bitrateKbps
                                }
                            }

                            lastIndexValue = currentIndex
                        }

                        // ::onActivated must be used, as it only listens for when the index is changed by a human
                        onActivated : {
                            if (resolutionListModel.get(currentIndex).is_custom) {
                                customResolutionDialog.open()
                            }
                            else {
                                updateBitrateForSelection()
                            }
                        }

                        NavigableDialog {
                            id: customResolutionDialog
                            standardButtons: Dialog.Ok | Dialog.Cancel
                            onOpened: {
                                // Force keyboard focus on the textbox so keyboard navigation works
                                widthField.forceActiveFocus()

                                // standardButton() was added in Qt 5.10, so we must check for it first
                                if (customResolutionDialog.standardButton) {
                                    customResolutionDialog.standardButton(Dialog.Ok).enabled = customResolutionDialog.isInputValid()
                                }
                            }

                            onClosed: {
                                widthField.clear()
                                heightField.clear()
                            }

                            onRejected: {
                                resolutionComboBox.currentIndex = resolutionComboBox.lastIndexValue
                            }

                            function isInputValid() {
                                // If we have text in either textbox that isn't valid,
                                // reject the input.
                                if ((!widthField.acceptableInput && widthField.text) ||
                                        (!heightField.acceptableInput && heightField.text)) {
                                    return false
                                }

                                // The textboxes need to have text or placeholder text
                                if ((!widthField.text && !widthField.placeholderText) ||
                                        (!heightField.text && !heightField.placeholderText)) {
                                    return false
                                }

                                return true
                            }

                            onAccepted: {
                                // Reject if there's invalid input
                                if (!isInputValid()) {
                                    reject()
                                    return
                                }

                                var width = widthField.text ? widthField.text : widthField.placeholderText
                                var height = heightField.text ? heightField.text : heightField.placeholderText

                                // Find and update the custom entry
                                for (var i = 0; i < resolutionListModel.count; i++) {
                                    if (resolutionListModel.get(i).is_custom) {
                                        resolutionListModel.setProperty(i, "video_width", width)
                                        resolutionListModel.setProperty(i, "video_height", height)
                                        resolutionListModel.setProperty(i, "text", "Custom ("+width+"x"+height+")")

                                        // Now update the bitrate using the custom resolution
                                        resolutionComboBox.currentIndex = i
                                        resolutionComboBox.updateBitrateForSelection()

                                        // Update the combobox width too
                                        resolutionComboBox.recalculateWidth()
                                        break
                                    }
                                }
                            }

                            ColumnLayout {
                                Label {
                                    text: qsTr("Custom resolutions are not officially supported by GeForce Experience, so it will not set your host display resolution. You will need to set it manually while in game.") + "\n\n" +
                                          qsTr("Resolutions that are not supported by your client or host PC may cause streaming errors.") + "\n"
                                    wrapMode: Label.WordWrap
                                    Layout.maximumWidth: 300
                                }

                                Label {
                                    text: qsTr("Enter a custom resolution:")
                                    font.bold: true
                                }

                                RowLayout {
                                    TextField {
                                        id: widthField
                                        maximumLength: 5
                                        inputMethodHints: Qt.ImhDigitsOnly
                                        placeholderText: resolutionListModel.get(resolutionComboBox.currentIndex).video_width
                                        validator: IntValidator{bottom:256; top:8192}
                                        focus: true

                                        onTextChanged: {
                                            // standardButton() was added in Qt 5.10, so we must check for it first
                                            if (customResolutionDialog.standardButton) {
                                                customResolutionDialog.standardButton(Dialog.Ok).enabled = customResolutionDialog.isInputValid()
                                            }
                                        }

                                        Keys.onReturnPressed: {
                                            customResolutionDialog.accept()
                                        }

                                        Keys.onEnterPressed: {
                                            customResolutionDialog.accept()
                                        }
                                    }

                                    Label {
                                        text: "x"
                                        font.bold: true
                                    }

                                    TextField {
                                        id: heightField
                                        maximumLength: 5
                                        inputMethodHints: Qt.ImhDigitsOnly
                                        placeholderText: resolutionListModel.get(resolutionComboBox.currentIndex).video_height
                                        validator: IntValidator{bottom:256; top:8192}

                                        onTextChanged: {
                                            // standardButton() was added in Qt 5.10, so we must check for it first
                                            if (customResolutionDialog.standardButton) {
                                                customResolutionDialog.standardButton(Dialog.Ok).enabled = customResolutionDialog.isInputValid()
                                            }
                                        }

                                        Keys.onReturnPressed: {
                                            customResolutionDialog.accept()
                                        }

                                        Keys.onEnterPressed: {
                                            customResolutionDialog.accept()
                                        }
                                    }
                                }
                            }
                        }
                    }

                    AutoResizingComboBox {
                        property int lastIndexValue

                        function updateBitrateForSelection() {
                            // Only modify the bitrate if the values actually changed
                            var selectedFps = parseInt(model.get(fpsComboBox.currentIndex).video_fps)
                            if (settingsPage.preferences.fps !== selectedFps) {
                                settingsPage.preferences.fps = selectedFps

                                if (settingsPage.preferences.autoAdjustBitrate) {
                                    settingsPage.preferences.bitrateKbps = StreamingPreferences.getDefaultBitrate(settingsPage.preferences.width,
                                                                                                              settingsPage.preferences.height,
                                                                                                              settingsPage.preferences.fps,
                                                                                                              settingsPage.preferences.enableYUV444);
                                    slider.value = settingsPage.preferences.bitrateKbps
                                }
                            }

                            lastIndexValue = currentIndex
                        }

                        NavigableDialog {
                            function isInputValid() {
                                // If we have text that isn't valid, reject the input.
                                if (!fpsField.acceptableInput && fpsField.text) {
                                    return false
                                }

                                // The textbox needs to have text or placeholder text
                                if (!fpsField.text && !fpsField.placeholderText) {
                                    return false
                                }

                                return true
                            }

                            id: customFpsDialog
                            standardButtons: Dialog.Ok | Dialog.Cancel
                            onOpened: {
                                // Force keyboard focus on the textbox so keyboard navigation works
                                fpsField.forceActiveFocus()

                                // standardButton() was added in Qt 5.10, so we must check for it first
                                if (customFpsDialog.standardButton) {
                                    customFpsDialog.standardButton(Dialog.Ok).enabled = customFpsDialog.isInputValid()
                                }
                            }

                            onClosed: {
                                fpsField.clear()
                            }

                            onRejected: {
                                fpsComboBox.currentIndex = fpsComboBox.lastIndexValue
                            }

                            onAccepted: {
                                // Reject if there's invalid input
                                if (!isInputValid()) {
                                    reject()
                                    return
                                }

                                var fps = fpsField.text ? fpsField.text : fpsField.placeholderText

                                // Find and update the custom entry
                                for (var i = 0; i < fpsListModel.count; i++) {
                                    if (fpsListModel.get(i).is_custom) {
                                        fpsListModel.setProperty(i, "video_fps", fps)
                                        fpsListModel.setProperty(i, "text", qsTr("Custom (%1 FPS)").arg(fps))

                                        // Now update the bitrate using the custom resolution
                                        fpsComboBox.currentIndex = i
                                        fpsComboBox.updateBitrateForSelection()

                                        // Update the combobox width too
                                        fpsComboBox.recalculateWidth()
                                        break
                                    }
                                }
                            }

                            ColumnLayout {
                                Label {
                                    text: qsTr("Enter a custom frame rate:")
                                    font.bold: true
                                }

                                RowLayout {
                                    TextField {
                                        id: fpsField
                                        maximumLength: 4
                                        inputMethodHints: Qt.ImhDigitsOnly
                                        placeholderText: fpsListModel.get(fpsComboBox.currentIndex).video_fps
                                        validator: IntValidator{bottom:10; top:9999}
                                        focus: true

                                        onTextChanged: {
                                            // standardButton() was added in Qt 5.10, so we must check for it first
                                            if (customFpsDialog.standardButton) {
                                                customFpsDialog.standardButton(Dialog.Ok).enabled = customFpsDialog.isInputValid()
                                            }
                                        }

                                        Keys.onReturnPressed: {
                                            customFpsDialog.accept()
                                        }

                                        Keys.onEnterPressed: {
                                            customFpsDialog.accept()
                                        }
                                    }
                                }
                            }
                        }

                        function getRefreshRates() {
                            var refreshRates = []
                            for (var displayIndex = 0; ; displayIndex++) {
                                var refreshRate = SystemProperties.getRefreshRate(displayIndex)
                                if (refreshRate === 0) {
                                    break
                                }

                                refreshRates.push(refreshRate)
                            }

                            return refreshRates
                        }

                        function choiceText(choice) {
                            switch (choice.kind) {
                            case "vrr":
                                return qsTr("VRR (%1 FPS)").arg(choice.video_fps)
                            case "low-latency-vrr":
                                return qsTr("Low-latency VRR (%1 FPS)").arg(choice.video_fps)
                            case "custom":
                                return qsTr("Custom (%1 FPS)").arg(choice.video_fps)
                            default:
                                return qsTr("%1 FPS").arg(choice.video_fps)
                            }
                        }

                        function reinitialize() {
                            var choices = settingsPage.preferences.getFpsChoices(getRefreshRates())
                            model.clear()
                            var hasCustomChoice = false

                            for (var i = 0; i < choices.length; i++) {
                                var choice = choices[i]
                                hasCustomChoice = hasCustomChoice || choice.is_custom
                                model.append({
                                                 "text": choiceText(choice),
                                                 "video_fps": choice.video_fps,
                                                 "is_custom": choice.is_custom
                                             })
                            }

                            var selectedFps = settingsPage.preferences.fps
                            var found = false
                            for (var j = 0; j < model.count; j++) {
                                if (selectedFps === parseInt(model.get(j).video_fps)) {
                                    currentIndex = j
                                    found = true
                                    break
                                }
                            }

                            if (!found) {
                                currentIndex = model.count > 0 ? 0 : -1
                            }

                            if (!hasCustomChoice) {
                                model.append({
                                                 "text": qsTr("Custom"),
                                                 "video_fps": "",
                                                 "is_custom": true
                                             })
                            }

                            recalculateWidth()
                            lastIndexValue = currentIndex
                        }

                        // ignore setting the index at first, and actually set it when the component is loaded
                        Component.onCompleted: {
                            reinitialize()
                            languageChanged.connect(reinitialize)
                        }

                        property bool vrrEnabled: settingsPage.preferences.enableVrr
                        onVrrEnabledChanged: reinitialize()
                        property bool vsyncEnabled: settingsPage.preferences.enableVsync
                        onVsyncEnabledChanged: reinitialize()

                        model: ListModel {
                            id: fpsListModel
                            // Populated by reinitialize().
                        }

                        id: fpsComboBox
                        maximumWidth: parent.width / 2
                        textRole: "text"
                        // ::onActivated must be used, as it only listens for when the index is changed by a human
                        onActivated : {
                            if (model.get(currentIndex).is_custom) {
                                customFpsDialog.open()
                            }
                            else {
                                updateBitrateForSelection()
                            }
                        }
                    }
                }

                Label {
                    width: parent.width
                    id: bitrateTitle
                    text: qsTr("Video bitrate:")
                    font.pointSize: 12
                    wrapMode: Text.Wrap
                }

                Label {
                    width: parent.width
                    id: bitrateDesc
                    text: qsTr("Lower the bitrate on slower connections. Raise the bitrate to increase image quality.")
                    font.pointSize: 9
                    wrapMode: Text.Wrap
                }

                Row {
                    width: parent.width
                    spacing: 5

                    Slider {
                        id: slider

                        value: settingsPage.preferences.bitrateKbps

                        stepSize: 500
                        from : 500
                        to: settingsPage.preferences.unlockBitrate ? 500000 : 150000

                        snapMode: "SnapOnRelease"
                        width: Math.min(bitrateDesc.implicitWidth, parent.width - (resetBitrateButton.visible ? resetBitrateButton.width + parent.spacing : 0))

                        onValueChanged: {
                            bitrateTitle.text = qsTr("Video bitrate: %1 Mbps").arg(value / 1000.0)
                            if (!settingsPage.refreshingProfileControls) {
                                settingsPage.preferences.bitrateKbps = value
                            }
                        }

                        onMoved: {
                            settingsPage.preferences.autoAdjustBitrate = false
                        }

                        Component.onCompleted: {
                            // Refresh the text after translations change
                            languageChanged.connect(valueChanged)
                        }
                    }

                    Button {
                        id: resetBitrateButton
                        text: qsTr("Use Default (%1 Mbps)").arg(StreamingPreferences.getDefaultBitrate(settingsPage.preferences.width, settingsPage.preferences.height, settingsPage.preferences.fps, settingsPage.preferences.enableYUV444) / 1000.0)
                        visible: settingsPage.preferences.bitrateKbps !== StreamingPreferences.getDefaultBitrate(settingsPage.preferences.width, settingsPage.preferences.height, settingsPage.preferences.fps, settingsPage.preferences.enableYUV444)
                        onClicked: {
                            var defaultBitrate = StreamingPreferences.getDefaultBitrate(settingsPage.preferences.width, settingsPage.preferences.height, settingsPage.preferences.fps, settingsPage.preferences.enableYUV444)
                            settingsPage.preferences.bitrateKbps = defaultBitrate
                            settingsPage.preferences.autoAdjustBitrate = true
                            slider.value = defaultBitrate
                        }
                    }
                }

                Label {
                    width: parent.width
                    id: windowModeTitle
                    text: qsTr("Display mode")
                    font.pointSize: 12
                    wrapMode: Text.Wrap
                    visible: SystemProperties.hasDesktopEnvironment
                }

                AutoResizingComboBox {
                    function createModel() {
                        var model = Qt.createQmlObject('import QtQuick 2.0; ListModel {}', parent, '')

                        model.append({
                                         text: qsTr("Fullscreen"),
                                         val: StreamingPreferences.WM_FULLSCREEN
                                     })

                        model.append({
                                         text: qsTr("Borderless windowed"),
                                         val: StreamingPreferences.WM_FULLSCREEN_DESKTOP
                                     })

                        model.append({
                                         text: qsTr("Windowed"),
                                         val: StreamingPreferences.WM_WINDOWED
                                     })


                        // Set the recommended option based on the OS
                        for (var i = 0; i < model.count; i++) {
                            var thisWm = model.get(i).val;
                            if (thisWm === StreamingPreferences.recommendedFullScreenMode) {
                                model.get(i).text += " " + qsTr("(Recommended)")
                                model.move(i, 0, 1)
                                break
                            }
                        }

                        return model
                    }


                    // This is used on initialization and upon retranslation
                    function reinitialize() {
                        if (!visible) {
                            // Do nothing if the control won't even be visible
                            return
                        }

                        model = createModel()
                        selectSavedValue()
                    }

                    function selectSavedValue() {
                        currentIndex = 0

                        // Set the current value based on the saved preferences
                        var savedWm = vrrForced ?
                                StreamingPreferences.WM_FULLSCREEN_DESKTOP :
                                settingsPage.preferences.windowMode
                        for (var i = 0; i < model.count; i++) {
                             var thisWm = model.get(i).val;
                             if (savedWm === thisWm) {
                                 currentIndex = i
                                 break
                             }
                        }

                    }

                    Component.onCompleted: {
                        reinitialize()
                        languageChanged.connect(reinitialize)
                    }

                    property bool vrrForced: settingsPage.preferences.enableVsync &&
                                             settingsPage.preferences.enableVrr
                    onVrrForcedChanged: reinitialize()

                    id: windowModeComboBox
                    visible: SystemProperties.hasDesktopEnvironment
                    enabled: !SystemProperties.rendererAlwaysFullScreen &&
                             !vrrForced
                    hoverEnabled: true
                    textRole: "text"
                    onActivated: {
                        settingsPage.preferences.windowMode = model.get(currentIndex).val
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: vrrForced ?
                                      qsTr("Borderless windowed mode is required for active VRR streaming. Your saved display mode will be restored for non-VRR sessions.")
                                    :
                                      qsTr("Fullscreen generally provides the best performance, but borderless windowed may work better with features like macOS Spaces, Alt+Tab, screenshot tools, on-screen overlays, etc.")
                }

                CheckBox {
                    id: vsyncCheck
                    width: parent.width
                    hoverEnabled: true
                    text: qsTr("V-Sync")
                    font.pointSize:  12
                    checked: settingsPage.preferences.enableVsync
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.enableVsync = checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("Disabling V-Sync allows sub-frame rendering latency, but it can display visible tearing")
                }

                CheckBox {
                    id: framePacingCheck
                    width: parent.width
                    hoverEnabled: true
                    text: qsTr("Frame pacing")
                    font.pointSize:  12
                    enabled: settingsPage.preferences.enableVsync
                    checked: settingsPage.preferences.enableVsync && settingsPage.preferences.framePacing
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.framePacing = checked
                        }
                    }
                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("Frame pacing reduces micro-stutter by delaying frames that come in too early")
                }
            }
        }

        GroupBox {

            id: vrrSettingsGroupBox
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" + qsTr("Variable Refresh Rate (VRR)") + "</font>"
            font.pointSize: 12

            Column {
                anchors.fill: parent
                spacing: 5

                CheckBox {
                    id: enableVrrCheck
                    width: parent.width
                    hoverEnabled: true
                    text: qsTr("Enable VRR")
                    font.pointSize: 12
                    enabled: settingsPage.preferences.enableVsync
                    checked: settingsPage.preferences.enableVsync &&
                             settingsPage.preferences.enableVrr
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.enableVrr = checked
                        }
                    }
                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: enabled ?
                                      qsTr("VRR uses paced adaptive presentation with best-effort tear avoidance. Sessions without enough refresh-rate headroom use fixed V-Sync. Borderless fullscreen is used while VRR is active.")
                                    :
                                      qsTr("VRR requires V-Sync. Enable V-Sync to change this setting.")
                }
            }
        }

        GroupBox {

            id: audioSettingsGroupBox
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" + qsTr("Audio Settings") + "</font>"
            font.pointSize: 12

            Column {
                anchors.fill: parent
                spacing: 5

                Label {
                    width: parent.width
                    id: resAudioTitle
                    text: qsTr("Audio configuration")
                    font.pointSize: 12
                    wrapMode: Text.Wrap
                }

                AutoResizingComboBox {
                    function reinitialize() {
                        var saved_audio = settingsPage.preferences.audioConfig
                        currentIndex = 0
                        for (var i = 0; i < audioListModel.count; i++) {
                            var el_audio = audioListModel.get(i).val;
                            if (saved_audio === el_audio) {
                                currentIndex = i
                                break
                            }
                        }
                    }

                    // ignore setting the index at first, and actually set it when the component is loaded
                    Component.onCompleted: {
                        reinitialize()
                    }

                    id: audioComboBox
                    textRole: "text"
                    model: ListModel {
                        id: audioListModel
                        ListElement {
                            text: qsTr("Stereo")
                            val: StreamingPreferences.AC_STEREO
                        }
                        ListElement {
                            text: qsTr("5.1 surround sound")
                            val: StreamingPreferences.AC_51_SURROUND
                        }
                        ListElement {
                            text: qsTr("7.1 surround sound")
                            val: StreamingPreferences.AC_71_SURROUND
                        }
                    }
                    // ::onActivated must be used, as it only listens for when the index is changed by a human
                    onActivated : {
                        settingsPage.preferences.audioConfig = audioListModel.get(currentIndex).val
                    }
                }


                CheckBox {
                    id: audioPcCheck
                    width: parent.width
                    text: qsTr("Mute host PC speakers while streaming")
                    font.pointSize: 12
                    checked: !settingsPage.preferences.playAudioOnHost
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.playAudioOnHost = !checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("You must restart any game currently in progress for this setting to take effect")
                }

                CheckBox {
                    id: muteOnFocusLossCheck
                    width: parent.width
                    text: qsTr("Mute audio stream when Moonlight is not the active window")
                    font.pointSize: 12
                    visible: SystemProperties.hasDesktopEnvironment
                    checked: settingsPage.preferences.muteOnFocusLoss
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.muteOnFocusLoss = checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("Mutes Moonlight's audio when you Alt+Tab out of the stream or click on a different window.")
                }
            }
        }

        GroupBox {
            id: hostSettingsGroupBox
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" + qsTr("Host Settings") + "</font>"
            font.pointSize: 12

            Column {
                anchors.fill: parent
                spacing: 5

                CheckBox {
                    id: optimizeGameSettingsCheck
                    width: parent.width
                    text: qsTr("Optimize game settings for streaming")
                    font.pointSize:  12
                    checked: settingsPage.preferences.gameOptimizations
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.gameOptimizations = checked
                        }
                    }
                }

                CheckBox {
                    id: quitAppAfter
                    width: parent.width
                    text: qsTr("Quit app on host PC after ending stream")
                    font.pointSize: 12
                    checked: settingsPage.preferences.quitAppAfter
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.quitAppAfter = checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("This will close the app or game you are streaming when you end your stream. You will lose any unsaved progress!")
                }
            }
        }

        GroupBox {
            id: uiSettingsGroupBox
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" + qsTr("Stream Behavior") + "</font>"
            font.pointSize: 12

            Column {
                anchors.fill: parent
                spacing: 5

                CheckBox {
                    id: connectionWarningsCheck
                    width: parent.width
                    text: qsTr("Show connection quality warnings")
                    font.pointSize: 12
                    checked: settingsPage.preferences.connectionWarnings
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.connectionWarnings = checked
                        }
                    }
                }

                CheckBox {
                    id: configurationWarningsCheck
                    width: parent.width
                    text: qsTr("Show configuration warnings")
                    font.pointSize: 12
                    checked: settingsPage.preferences.configurationWarnings
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.configurationWarnings = checked
                        }
                    }
                }

                CheckBox {
                    id: keepAwakeCheck
                    width: parent.width
                    text: qsTr("Keep the display awake while streaming")
                    font.pointSize: 12
                    checked: settingsPage.preferences.keepAwake
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.keepAwake = checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("Prevents the screensaver from starting or the display from going to sleep while streaming.")
                }
            }
        }
    }

    Column {
        padding: 10
        rightPadding: 20
        anchors.left: settingsColumn1.right
        id: settingsColumn2
        width: settingsPage.width / 2
        spacing: 15

        GroupBox {
            id: inputSettingsGroupBox
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" + qsTr("Input Settings") + "</font>"
            font.pointSize: 12

            Column {
                anchors.fill: parent
                spacing: 5

                CheckBox {
                    id: absoluteMouseCheck
                    hoverEnabled: true
                    width: parent.width
                    text: qsTr("Optimize mouse for remote desktop instead of games")
                    font.pointSize:  12
                    checked: settingsPage.preferences.absoluteMouseMode
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.absoluteMouseMode = checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 10000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("This enables seamless mouse control without capturing the client's mouse cursor. It is ideal for remote desktop usage but will not work in most games.") + " " +
                                  qsTr("You can toggle this while streaming using Ctrl+Alt+Shift+M.") + "\n\n" +
                                  qsTr("NOTE: Due to a bug in GeForce Experience, this option may not work properly if your host PC has multiple monitors.")
                }

                Row {
                    spacing: 5
                    width: parent.width

                    CheckBox {
                        id: captureSysKeysCheck
                        hoverEnabled: true
                        text: qsTr("Capture system keyboard shortcuts")
                        font.pointSize: 12
                        enabled: SystemProperties.hasDesktopEnvironment
                        checked: settingsPage.preferences.captureSysKeysMode !== StreamingPreferences.CSK_OFF || !SystemProperties.hasDesktopEnvironment

                        ToolTip.delay: 1000
                        ToolTip.timeout: 10000
                        ToolTip.visible: hovered
                        ToolTip.text: qsTr("This enables the capture of system-wide keyboard shortcuts like Alt+Tab that would normally be handled by the client OS while streaming.") + "\n\n" +
                                      qsTr("NOTE: Certain keyboard shortcuts like Ctrl+Alt+Del on Windows cannot be intercepted by any application, including Moonlight.")
                    }

                    AutoResizingComboBox {
                        id: captureSysKeysModeComboBox

                        function reinitialize() {
                            if (!visible) {
                                return
                            }

                            var saved_syskeysmode = settingsPage.preferences.captureSysKeysMode
                            currentIndex = 0
                            for (var i = 0; i < captureSysKeysModeListModel.count; i++) {
                                var el_syskeysmode = captureSysKeysModeListModel.get(i).val;
                                if (saved_syskeysmode === el_syskeysmode) {
                                    currentIndex = i
                                    break
                                }
                            }
                        }

                        // ignore setting the index at first, and actually set it when the component is loaded
                        Component.onCompleted: {
                            reinitialize()
                        }

                        enabled: captureSysKeysCheck.checked && captureSysKeysCheck.enabled
                        textRole: "text"
                        model: ListModel {
                            id: captureSysKeysModeListModel
                            ListElement {
                                text: qsTr("in fullscreen")
                                val: StreamingPreferences.CSK_FULLSCREEN
                            }
                            ListElement {
                                text: qsTr("always")
                                val: StreamingPreferences.CSK_ALWAYS
                            }
                        }

                        function updatePref() {
                            if (!enabled) {
                                settingsPage.preferences.captureSysKeysMode = StreamingPreferences.CSK_OFF
                            }
                            else {
                                settingsPage.preferences.captureSysKeysMode = captureSysKeysModeListModel.get(currentIndex).val
                            }
                        }

                        // ::onActivated must be used, as it only listens for when the index is changed by a human
                        onActivated: {
                            updatePref()
                        }

                        // This handles transition of the checkbox state
                        onEnabledChanged: {
                            if (!settingsPage.refreshingProfileControls) {
                                updatePref()
                            }
                        }
                    }
                }

                CheckBox {
                    id: absoluteTouchCheck
                    hoverEnabled: true
                    width: parent.width
                    text: qsTr("Use touchscreen as a virtual trackpad")
                    font.pointSize:  12
                    checked: !settingsPage.preferences.absoluteTouchMode
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.absoluteTouchMode = !checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("When checked, the touchscreen acts like a trackpad. When unchecked, the touchscreen will directly control the mouse pointer.")
                }

                CheckBox {
                    id: swapMouseButtonsCheck
                    hoverEnabled: true
                    width: parent.width
                    text: qsTr("Swap left and right mouse buttons")
                    font.pointSize:  12
                    checked: settingsPage.preferences.swapMouseButtons
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.swapMouseButtons = checked
                        }
                    }
                }

                CheckBox {
                    id: reverseScrollButtonsCheck
                    hoverEnabled: true
                    width: parent.width
                    text: qsTr("Reverse mouse scrolling direction")
                    font.pointSize: 12
                    checked: settingsPage.preferences.reverseScrollDirection
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.reverseScrollDirection = checked
                        }
                    }
                }
            }
        }

        GroupBox {
            id: gamepadSettingsGroupBox
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" + qsTr("Gamepad Settings") + "</font>"
            font.pointSize: 12

            Column {
                anchors.fill: parent
                spacing: 5

                CheckBox {
                    id: swapFaceButtonsCheck
                    width: parent.width
                    text: qsTr("Swap A/B and X/Y gamepad buttons")
                    font.pointSize: 12
                    checked: settingsPage.preferences.swapFaceButtons
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.swapFaceButtons = checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("This switches gamepads into a Nintendo-style button layout")
                }

                CheckBox {
                    id: singleControllerCheck
                    width: parent.width
                    text: qsTr("Force gamepad #1 always connected")
                    font.pointSize:  12
                    checked: !settingsPage.preferences.multiController
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.multiController = !checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("Forces a single gamepad to always stay connected to the host, even if no gamepads are actually connected to this PC.") + " " +
                                  qsTr("Only enable this option when streaming a game that doesn't support gamepads being connected after startup.")
                }

                CheckBox {
                    id: gamepadMouseCheck
                    hoverEnabled: true
                    width: parent.width
                    text: qsTr("Enable mouse control with gamepads by holding the 'Start' button")
                    font.pointSize: 12
                    checked: settingsPage.preferences.gamepadMouse
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.gamepadMouse = checked
                        }
                    }
                }

                CheckBox {
                    id: backgroundGamepadCheck
                    width: parent.width
                    text: qsTr("Process gamepad input when Moonlight is in the background")
                    font.pointSize: 12
                    visible: SystemProperties.hasDesktopEnvironment
                    checked: settingsPage.preferences.backgroundGamepad
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.backgroundGamepad = checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("Allows Moonlight to capture gamepad inputs even if it's not the current window in focus")
                }
            }
        }

        GroupBox {
            id: advancedSettingsGroupBox
            width: (parent.width - (parent.leftPadding + parent.rightPadding))
            padding: 12
            title: "<font color=\"skyblue\">" + qsTr("Advanced Settings") + "</font>"
            font.pointSize: 12

            Column {
                anchors.fill: parent
                spacing: 5

                Label {
                    width: parent.width
                    id: resVDSTitle
                    text: qsTr("Video decoder")
                    font.pointSize: 12
                    wrapMode: Text.Wrap
                }

                AutoResizingComboBox {
                    function reinitialize() {
                        var saved_vds = settingsPage.preferences.videoDecoderSelection
                        currentIndex = 0
                        for (var i = 0; i < decoderListModel.count; i++) {
                            var el_vds = decoderListModel.get(i).val;
                            if (saved_vds === el_vds) {
                                currentIndex = i
                                break
                            }
                        }
                    }

                    // ignore setting the index at first, and actually set it when the component is loaded
                    Component.onCompleted: {
                        reinitialize()
                    }

                    id: decoderComboBox
                    textRole: "text"
                    model: ListModel {
                        id: decoderListModel
                        ListElement {
                            text: qsTr("Automatic (Recommended)")
                            val: StreamingPreferences.VDS_AUTO
                        }
                        ListElement {
                            text: qsTr("Force software decoding")
                            val: StreamingPreferences.VDS_FORCE_SOFTWARE
                        }
                        ListElement {
                            text: qsTr("Force hardware decoding")
                            val: StreamingPreferences.VDS_FORCE_HARDWARE
                        }
                    }
                    // ::onActivated must be used, as it only listens for when the index is changed by a human
                    onActivated: {
                        if (enabled) {
                            settingsPage.preferences.videoDecoderSelection = decoderListModel.get(currentIndex).val
                        }
                    }
                }

                Label {
                    width: parent.width
                    id: resVCCTitle
                    text: qsTr("Video codec")
                    font.pointSize: 12
                    wrapMode: Text.Wrap
                }

                AutoResizingComboBox {
                    function reinitialize() {
                        var saved_vcc = settingsPage.preferences.videoCodecConfig

                        // Default to Automatic (relevant if HDR is enabled,
                        // where we will match none of the codecs in the list)
                        currentIndex = 0

                        for (var i = 0; i < codecListModel.count; i++) {
                            var el_vcc = codecListModel.get(i).val;
                            if (saved_vcc === el_vcc) {
                                currentIndex = i
                                break
                            }
                        }
                    }

                    // ignore setting the index at first, and actually set it when the component is loaded
                    Component.onCompleted: {
                        reinitialize()
                    }

                    id: codecComboBox
                    textRole: "text"
                    model: ListModel {
                        id: codecListModel
                        ListElement {
                            text: qsTr("Automatic (Recommended)")
                            val: StreamingPreferences.VCC_AUTO
                        }
                        ListElement {
                            text: qsTr("H.264")
                            val: StreamingPreferences.VCC_FORCE_H264
                        }
                        ListElement {
                            text: qsTr("HEVC (H.265)")
                            val: StreamingPreferences.VCC_FORCE_HEVC
                        }
                        ListElement {
                            text: qsTr("AV1 (Experimental)")
                            val: StreamingPreferences.VCC_FORCE_AV1
                        }
                    }
                    // ::onActivated must be used, as it only listens for when the index is changed by a human
                    onActivated : {
                        if (enabled) {
                            settingsPage.preferences.videoCodecConfig = codecListModel.get(currentIndex).val
                        }
                    }
                }

                CheckBox {
                    id: enableHdr
                    width: parent.width
                    text: qsTr("Enable HDR (Experimental)")
                    font.pointSize: 12

                    enabled: SystemProperties.supportsHdr
                    checked: enabled && settingsPage.preferences.enableHdr
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.enableHdr = checked
                        }
                    }

                    // Updating settingsPage.preferences.videoCodecConfig is handled above

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: enabled ?
                                      qsTr("The stream will be HDR-capable, but some games may require an HDR monitor on your host PC to enable HDR mode.")
                                    :
                                      qsTr("HDR streaming is not supported on this PC.")
                }

                CheckBox {
                    id: enableYUV444
                    width: parent.width
                    text: qsTr("Enable YUV 4:4:4 (Experimental)")
                    font.pointSize: 12

                    checked: settingsPage.preferences.enableYUV444
                    onCheckedChanged: {
                        if (settingsPage.refreshingProfileControls) {
                            return
                        }

                        // This is called on init, so only reset to default bitrate when checked state changes.
                        if (settingsPage.preferences.enableYUV444 != checked) {
                            settingsPage.preferences.enableYUV444 = checked
                            if (settingsPage.preferences.autoAdjustBitrate) {
                                settingsPage.preferences.bitrateKbps = StreamingPreferences.getDefaultBitrate(settingsPage.preferences.width,
                                                                                                          settingsPage.preferences.height,
                                                                                                          settingsPage.preferences.fps,
                                                                                                          settingsPage.preferences.enableYUV444);
                                slider.value = settingsPage.preferences.bitrateKbps
                            }
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: enabled ?
                                      qsTr("Good for streaming desktop and text-heavy games, but not recommended for fast-paced games.")
                                    :
                                      qsTr("YUV 4:4:4 is not supported on this PC.")
                }

                CheckBox {
                    id: unlockBitrate
                    width: parent.width
                    text: qsTr("Unlock bitrate limit (Experimental)")
                    font.pointSize: 12

                    checked: settingsPage.preferences.unlockBitrate
                    onCheckedChanged: {
                        if (settingsPage.refreshingProfileControls) {
                            return
                        }

                        settingsPage.preferences.unlockBitrate = checked
                        settingsPage.preferences.bitrateKbps = Math.min(settingsPage.preferences.bitrateKbps, slider.to)
                        slider.value = settingsPage.preferences.bitrateKbps
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("This unlocks extremely high video bitrates for use with Sunshine hosts. It should only be used when streaming over an Ethernet LAN connection.")
                }

                CheckBox {
                    id: showPerformanceOverlay
                    width: parent.width
                    text: qsTr("Show performance stats while streaming")
                    font.pointSize: 12
                    checked: settingsPage.preferences.showPerformanceOverlay
                    onCheckedChanged: {
                        if (!settingsPage.refreshingProfileControls) {
                            settingsPage.preferences.showPerformanceOverlay = checked
                        }
                    }

                    ToolTip.delay: 1000
                    ToolTip.timeout: 5000
                    ToolTip.visible: hovered
                    ToolTip.text: qsTr("Display real-time stream performance information while streaming.") + "\n\n" +
                                  qsTr("You can toggle it at any time while streaming using Ctrl+Alt+Shift+S or Select+L1+R1+X.") + "\n\n" +
                                  qsTr("The performance overlay is not supported on Steam Link or Raspberry Pi.")
                }
            }
        }
    }

    NavigableDialog {
        id: profilePickerDialog
        property var profileEntries: []
        title: qsTr("Select Streaming Profile")
        standardButtons: Dialog.Cancel

        function refresh() {
            profileEntries =
                StreamProfileManager.profiles(profileEditor.hostUuid)
        }

        onOpened: {
            if (settingsProfileButtons.count > 0) {
                settingsProfileButtons.itemAt(0)
                    .forceActiveFocus(Qt.TabFocus)
            }
        }

        ColumnLayout {
            spacing: 5

            Repeater {
                id: settingsProfileButtons
                model: profilePickerDialog.profileEntries

                Button {
                    Layout.fillWidth: true
                    text: (modelData.active ? "● " : "") + modelData.name
                    highlighted: modelData.active
                    onClicked: {
                        profilePickerDialog.close()
                        settingsPage.requestProfileSwitch(modelData.profileId)
                    }
                }
            }

            Button {
                Layout.fillWidth: true
                opacity: 0.55
                text: qsTr("New Profile")
                onClicked: {
                    profilePickerDialog.close()
                    settingsPage.requestNewProfile()
                }
            }
        }
    }

    NavigableDialog {
        id: unsavedChangesDialog
        title: qsTr("Unsaved Changes")
        modal: true

        Label {
            text: qsTr("Save changes to this streaming profile?")
            wrapMode: Text.Wrap
        }

        footer: DialogButtonBox {
            Button {
                text: qsTr("Save")
                DialogButtonBox.buttonRole: DialogButtonBox.AcceptRole
                onClicked: {
                    unsavedChangesDialog.close()
                    settingsPage.saveAndClose()
                }
            }
            Button {
                text: qsTr("Discard")
                DialogButtonBox.buttonRole: DialogButtonBox.DestructiveRole
                onClicked: {
                    profileEditor.discardChanges()
                    unsavedChangesDialog.close()
                    settingsPage.finishCloseAction()
                }
            }
            Button {
                text: qsTr("Cancel")
                DialogButtonBox.buttonRole: DialogButtonBox.RejectRole
                onClicked: unsavedChangesDialog.close()
            }
        }
    }

    NavigableMessageDialog {
        id: deleteProfileDialog
        text: qsTr("Delete profile '%1'?").arg(profileEditor.name)
        standardButtons: Dialog.Yes | Dialog.No
        onAccepted: {
            if (profileEditor.remove()) {
                stackView.pop()
            }
        }
    }

    NavigableMessageDialog {
        id: invalidNameDialog
        text: qsTr("Profile names cannot be empty.")
        standardButtons: Dialog.Ok
    }
}
